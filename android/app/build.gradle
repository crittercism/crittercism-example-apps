apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "com.crittercism"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 5
        versionName "5.5.5"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        sdkonly {}
        ndk {}
    }

    lintOptions {
      abortOnError false
    }

    sourceSets.main {
        jni.srcDirs = [] //disable automatic ndk-build call
    }

    sourceSets.ndk {
        jniLibs.srcDir 'src/main/libs'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.3'

    sdkonlyCompile 'com.crittercism:crittercism-android-agent:5.5.5'
    ndkCompile 'com.crittercism:crittercism-android-ndk-agent:5.5.5'

    compile 'com.squareup.okhttp:okhttp:2.2.0'
}

task ndkBuild(type: Exec) {
    if (android.ndkDirectory == null) {
        throw new GradleException("please set ndk.dir in your local.properties file")
    } else {
        commandLine "${android.ndkDirectory.getAbsolutePath()}/ndk-build", '-C', file('src/main').absolutePath
    }
}

gradle.projectsEvaluated {
    compileNdkDebugJavaWithJavac.dependsOn ndkBuild
    compileNdkReleaseJavaWithJavac.dependsOn ndkBuild
}

clean {
    delete file("src/main/libs/armeabi").absolutePath
    delete file("src/main/libs/armeabi-v7a").absolutePath
    delete file("src/main/libs/arm64-v8a").absolutePath
    delete file("src/main/obj").absolutePath
}
